load("@gazelle//:def.bzl", "gazelle")
load("@io_bazel_rules_go//go:def.bzl", "go_binary", "go_library")
load("@rules_license//rules:license.bzl", "license")
load("@rules_pkg//:pkg.bzl", "pkg_zip")

package(
    default_applicable_licenses = ["//:license"],
    default_visibility = ["//visibility:public"],
)

licenses(["notice"])

license(
    name = "license",
    license_kinds = [
        "@rules_license//licenses/spdx:Apache-2.0",
    ],
    license_text = "LICENSE",
)

# gazelle:prefix github.com/GoogleCloudPlatform/proto-gen-md-diagrams
# gazelle:exclude test/protos/**
gazelle(name = "gazelle")

go_library(
    name = "main_lib",
    srcs = ["main.go"],
    importpath = "github.com/GoogleCloudPlatform/proto-gen-md-diagrams",
    visibility = ["//visibility:private"],
    deps = ["//pkg/proto"],
)

go_binary(
    name = "main",
    basename = "proto-gen-md-diagrams",
    embed = [":main_lib"],
    visibility = ["//visibility:public"],
)

archive_base_name = "proto-gen-md-diagrams"

pkg_zip(
    name = "main_zip",
    srcs = [
        "LICENSE",
        "NOTICE",
        "README.md",
        ":main",
    ],
    out = "binary.zip",
    package_file_name = select({
        "on_linux": "{}-linux-x86_64".format(archive_base_name),
        "on_osx_x64": "{}-osx-x86_64".format(archive_base_name),
        "on_windows": "{}-win-x86_64".format(archive_base_name),
        "on_osx_arm64": "{}-osx-arm64".format(archive_base_name),
    }),
)

config_setting(
    name = "on_linux",
    constraint_values = [
        "@platforms//os:linux",
        "@platforms//cpu:x86_64",
    ],
)

config_setting(
    name = "on_windows",
    constraint_values = [
        "@platforms//os:windows",
        "@platforms//cpu:x86_64",
    ],
)

config_setting(
    name = "on_osx_x64",
    constraint_values = [
        "@platforms//os:osx",
        "@platforms//cpu:x86_64",
    ],
)

config_setting(
    name = "on_osx_arm64",
    constraint_values = [
        "@platforms//os:osx",
        "@platforms//cpu:arm64",
    ],
)
